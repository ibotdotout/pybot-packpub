*** Settings ***
Library           Selenium2Library
Library           XvfbRobot
Resource          config.txt

*** Variables ***
${HOME URL}        https://www.packtpub.com/packt/offers/free-learning
${END URL}         https://www.packtpub.com/account/my-ebooks

${CLAIM BUTTON}    xpath=//*[@id="deal-of-the-day"]/div/div/div[2]/div[4]/div[2]/a/div/input
#${CLAIM BUTTON}    xpath=//*[@id="deal-of-the-day"]/div/div/div[2]/div[5]/div[2]/a/div/input
${LOGIN BUTTON}    xpath=//*[@id="account-bar-form-login"]/div[1]/form/div/div/div[3]/input
${USER INPUT}      xpath=//*[@id="account-bar-form-login"]/div[1]/form/div/div/div[1]/div/input
${PASS INPUT}      xpath=//*[@id="account-bar-form-login"]/div[1]/form/div/div/div[2]/div/input
${BOOK TITLE}      xpath=//*[@id="deal-of-the-day"]/div/div/div[2]/div[2]/h2

${LOGIN DONE}      My Account
${CLAIM DONE}      My eBooks

${WIDTH}           1920
${HIEGHT}          1080


*** Test Cases ***
Get Free Ebook
    Open Headless Browser    ${HOME URL}
    Location Should Be   ${HOME URL}
    Login     ${USER}     ${PASSWORD}
    ${book title}=     Get Book Title
    Claim Book
    Location Should Be    ${END URL}
    Page Should Contain    ${book title}


*** Keywords ***
Open Headless Browser
    [Arguments]    ${url}
    Start Virtual Display    ${WIDTH}   ${HIEGHT}
    Open Browser    ${url}
    Set Window Size    ${WIDTH}    ${HIEGHT}

Login
    [Arguments]    ${user}    ${pass}
    Click Button   ${CLAIM BUTTON}
    Input Text    ${USER INPUT}     ${user}
    Input Text    ${PASS INPUT}     ${pass}
    Click Button    ${LOGIN BUTTON}
    Wait Until Page Contains    ${LOGIN DONE}

Claim Book
    Click Button   ${CLAIM BUTTON}
    Wait Until Page Contains    ${CLAIM DONE}

Get Book Title
    ${book title}=    Get Text    ${BOOK TITLE}
    [return]    ${book title}
